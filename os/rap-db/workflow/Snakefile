configfile: "config.yaml"


include: "rules/download.smk"
include: "rules/id_converter.smk"


rule all:
    input:
        bwa = "results/genome/os.rap-db.genome.fa.sa",
        bowtie2 = "results/genome/os.rap-db.genome.fa.1.bt2",
        make_gtf = "results/gtf/os.rap-db.make.gtf",
        make_gff = "results/gff/os.rap-db.make.gff",
        star_index = "results/star_index",
        subread_index = "results/subread_index/subread_index.files",
        hisat2_index = "results/hisat2_index/hisat2_index.1.ht2",
        custom_bed = "results/custom_bed/transcript.bed",
        make_gene_info = "results/gene_info.tsv.gz",
        salmon_index = "results/salmon_index",
        igv_genome = "results/igv_genome/os.rap-db.genome.json"


rule genome:
    input:
        "rawdata/IRGSP-1.0_genome.fasta.gz"
    output:
        fa = "results/genome/os.rap-db.genome.fa",
        fai = "results/genome/os.rap-db.genome.fa.fai",
        dict = "results/genome/os.rap-db.genome.dict"
    shell:
        '''
        gzip -dc {input} > {output.fa}
        samtools faidx {output.fa}
        gatk CreateSequenceDictionary -R {output.fa}
        '''


rule bwa:
    input:
        "results/genome/os.rap-db.genome.fa"
    output:
        ["results/genome/os.rap-db.genome.fa" + suffix for suffix in [".amb", ".ann", ".bwt", ".pac", ".sa"]]
    shell:
        "bwa index {input}"


rule bowtie2:
    input:
        "results/genome/os.rap-db.genome.fa"
    output:
        ["results/genome/os.rap-db.genome.fa" + suffix for suffix in [".1.bt2", ".2.bt2", ".3.bt2", ".4.bt2", ".rev.1.bt2", ".rev.2.bt2"]]
    shell:
        "bowtie2-build {input} {input}"


rule cat_gff:
    input:
        "rawdata/IRGSP-1.0_representative/locus.gff",
        "rawdata/IRGSP-1.0_representative/transcripts_exon.gff",
        "rawdata/IRGSP-1.0_representative/transcripts.gff"
    output:
        "resources/cat.gff"
    shell:
        "cat {input} > {output}"


rule make_gtf:
    input:
        "resources/cat.gff"
    output:
        "results/gtf/os.rap-db.make.gtf"
    script:
        "scripts/make_gtf.R"


rule make_gff:
    input:
        "resources/cat.gff"
    output:
        "results/gff/os.rap-db.make.gff"
    script:
        "scripts/make_gff.R"


rule star_index:
    input:
        fa = "results/genome/os.rap-db.genome.fa",
        gtf = "results/gtf/os.rap-db.make.gtf"
    output:
        star_index = directory("results/star_index")
    threads:
        20
    shell:
        '''
        STAR \
            --runThreadN {threads} \
            --runMode genomeGenerate \
            --genomeDir {output.star_index} \
            --genomeFastaFiles {input.fa} \
            --sjdbGTFfile {input.gtf} \
            --genomeSAindexNbases 13  # Adjusted base on STAR's warning.
        '''


rule subread_index:
    input:
        fa = "results/genome/os.rap-db.genome.fa"
    output:
        "results/subread_index/subread_index.files"
    params:
        prefix = "results/subread_index/subread_index"
    shell:
        '''
        subread-buildindex -o {params.prefix} {input.fa}
        '''


rule hisat2_index:
    input:
        fa = "results/genome/os.rap-db.genome.fa"
    output:
        hisat2_index = "results/hisat2_index/hisat2_index.1.ht2"
    params:
        prefix = "results/hisat2_index/hisat2_index"
    shell:
        '''
        hisat2-build {input.fa} {params.prefix}
        '''


rule custom_bed:
    input:
        gtf = "results/gtf/os.rap-db.make.gtf",
        fa = "results/genome/os.rap-db.genome.fa"  # for seqlengths
    output:
        transcript = "results/custom_bed/transcript.bed",
        gene = "results/custom_bed/gene.bed",
        exon = "results/custom_bed/exon.bed",
        upstream_3kb = "results/custom_bed/upstream_3kb.bed",
    script:
        "scripts/custom_bed.R"


rule make_gene_info:
    input:
        rules.download_annotation.output
    output:
        "results/gene_info.tsv.gz"
    script:
        "scripts/make_gene_info.R"


rule salmon_index:
    input:
        rules.download_transcript.output
    output:
        directory("results/salmon_index")
    shell:
        "salmon index -t {input} -i {output}"


rule make_igv_genome:
    input:
        fa = "results/genome/os.rap-db.genome.fa",       # IGV can only read ungzipped FASTA
        fai = "results/genome/os.rap-db.genome.fa.fai",
        gff = "results/gff/os.rap-db.make.gff"           # IGV's search only function with ungzipped GFF/GTF
    output:
        outdir = directory("results/igv_genome"),
        json = "results/igv_genome/os.rap-db.genome.json"
    params:
        id =  "IRGSP-1.0",
        name = "O. sativa (IRGSP-1.0)",
        chromosome_order = ["chr01", "chr02", "chr03", "chr04", "chr05", "chr06", "chr07", "chr08", "chr09", "chr10", "chr11", "chr12"]
    script:
        "scripts/make_igv_genome.py"
