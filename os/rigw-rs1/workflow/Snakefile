

include: "rules/download.smk"


rule all:
    input:
        ["results/genome/MH63RS1.genome.fa" + suffix for suffix in [".amb", ".ann", ".bwt", ".pac", ".sa"]],
        gtf = "results/gtf/MH63RS1.make.gtf",
        best_hit = "results/id_converter/MH63RS1.to.rap-db.best-hit.tsv",


rule genome:
    input:
        "rawdata/MH63RS1.LNNK00000000.fsa"
    output:
        fa = "results/genome/MH63RS1.genome.fa",
        fai = "results/genome/MH63RS1.genome.fa.fai",
        dict = "results/genome/MH63RS1.genome.dict"
    params:
        chromosomes = "Chr{01..12}"
    shell:
        '''
        samtools faidx {input} {params.chromosomes} > {output.fa}
        samtools faidx {output.fa}
        gatk CreateSequenceDictionary -R {output.fa}
        '''


rule bwa:
    input:
        "results/genome/MH63RS1.genome.fa",
    output:
        ["results/genome/MH63RS1.genome.fa" + suffix for suffix in [".amb", ".ann", ".bwt", ".pac", ".sa"]]
    shell:
        "bwa index {input}"


rule make_gtf:
    input:
        gff = "rawdata/MH63_chr_v2.gff"
    output:
        gtf = "results/gtf/MH63RS1.make.gtf"
    params:
        chromosomes = ["Chr{:02d}".format(x) for x in range(1, 13)]
    script:
        "scripts/make_gtf.R"


rule longest_cds:
    input:
        raw_cds = "rawdata/MH63.RS1.CDS.fa"
    output:
        longest_cds = "results/longest_cds/MH63RS1.longest_cds.fa"
    script:
        "scripts/longest_cds.R"


rule diamond_blastx:
    input:
        cds = "results/longest_cds/MH63RS1.longest_cds.fa",
        diamond_db = "../rap-db/results/os.rap-db.dmnd"
    output:
        "results/diamond/MH63RS1.to.rap-db.tsv"
    shell:
        '''
        diamond blastx \
            -d {input.diamond_db} \
            -q {input.cds} \
            -o {output} \
            --very-sensitive
        '''


rule id_converter:
    input:
        diamond_output = "results/diamond/MH63RS1.to.rap-db.tsv",
    output:
        best_hit = "results/id_converter/MH63RS1.to.rap-db.best-hit.tsv",
    script:
        "scripts/id_converter.R"

