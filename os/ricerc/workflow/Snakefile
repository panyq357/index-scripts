

cultivar = ["ZH11", "9311", "KY131"]


rule all:
    input:
        expand("results/gtf/{cultivar}.make.gtf", cultivar=cultivar),
        expand("results/genome/{cultivar}.genome.fa.{suffix}", cultivar=cultivar, suffix=["amb", "ann", "bwt", "pac", "sa"]),
        expand("results/id_converter/{cultivar}.to.{ref}.best-hit.tsv", cultivar=cultivar, ref=["rap-db", "msu"])


rule genome:
    input:
        "rawdata/{variety}.genome.tar.gz"
    output:
        fa = "results/genome/{variety}.genome.fa",
        fai = "results/genome/{variety}.genome.fa.fai",
        dict = "results/genome/{variety}.genome.dict"
    shell:
        '''
        tar -xzf {input}
        samtools faidx {wildcards.variety}.genome Chr{{1..12}} > {output.fa}
        samtools faidx {output.fa}
        gatk CreateSequenceDictionary -R {output.fa}
        rm {wildcards.variety}.genome {wildcards.variety}.genome.fai
        '''

rule bwa:
    input:
        "results/genome/{variety}.genome.fa"
    output:
        ["results/genome/{variety}.genome.fa" + suffix for suffix in [".amb", ".ann", ".bwt", ".pac", ".sa"]]
    shell:
        "bwa index {input}"


rule make_gtf:
    input:
        "rawdata/{variety}.IGDBv1.Allset.gff.tar.gz"
    output:
        "results/gtf/{variety}.make.gtf",
        temp("{variety}/{variety}.IGDBv1.Allset.gff")
    script:
        "scripts/make_gtf.R"


rule longest_cds:
    input:
        cds_tar = "rawdata/{variety}.IGDBv1.Allset.cds.fasta.tar.gz",
    output:
        longest_cds = "results/longest_cds/{variety}.longest_cds.fa",
        untar = temp("{variety}/{variety}.IGDBv1.Allset.cds.fasta")
    script:
        "scripts/longest_cds.R"


rule diamond_blastx_to_rapdb:
    input:
        longest_cds = "results/longest_cds/{variety}.longest_cds.fa",
        d = "../rap-db/results/os.rap-db.dmnd"
    output:
        o = "results/diamond/{variety}.to.rap-db.tsv",
    shell:
        '''
        diamond blastx \
            -d {input.d} \
            -q {input.longest_cds} \
            -o {output.o} \
            --very-sensitive
        '''


rule id_converter_to_rapdb:
    input:
        diamond_output = "results/diamond/{variety}.to.rap-db.tsv",
    output:
        best_hit = "results/id_converter/{variety}.to.rap-db.best-hit.tsv",
    script:
        "scripts/id_converter.R"


rule diamond_blastx_to_msu:
    input:
        longest_cds = "results/longest_cds/{variety}.longest_cds.fa",
        d = "../msu/results/os.msu.dmnd"
    output:
        o = "results/diamond/{variety}.to.msu.tsv",
    shell:
        '''
        diamond blastx \
            -d {input.d} \
            -q {input.longest_cds} \
            -o {output.o} \
            --very-sensitive
        '''


rule id_converter_to_msu:
    input:
        diamond_output = "results/diamond/{variety}.to.msu.tsv",
    output:
        best_hit = "results/id_converter/{variety}.to.msu.best-hit.tsv",
    script:
        "scripts/id_converter.R"


